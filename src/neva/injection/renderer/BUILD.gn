# Copyright 2016-2019 LG Electronics, Inc.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
# SPDX-License-Identifier: Apache-2.0

import("//neva/neva.gni")
import("//tools/grit/repack.gni")

source_set("renderer") {
  deps = [
    "//base",
    "//gin",
    "//neva/app_runtime/public/mojom",
    "//neva/browser_shell/service/public/mojom",
    "//neva/injection/public/common:common_sources",
    "//neva/pal_service",
    "//neva/pal_service/public/mojom",
    "//third_party/blink/public:blink",
  ]

  public_deps = [ "//v8" ]

  sources = [
    "gin/function_template_neva.h",
    "file_name_validate.cc",
    "file_name_validate.h",
    "injection_browser_control_base.cc",
    "injection_browser_control_base.h",
    "injection_data_manager.cc",
    "injection_data_manager.h",
    "injection_events_emitter.cc",
    "injection_events_emitter.h",
  ]

  if (enable_browser_control_webapi) {
    sources += [
      "browser_control/browser_control_injection.cc",
      "browser_control/browser_control_injection.h",
      "browser_control/cookiemanager_injection.cc",
      "browser_control/cookiemanager_injection.h",
      "browser_control/customuseragent_injection.cc",
      "browser_control/customuseragent_injection.h",
      "browser_control/mediacapture_injection.cc",
      "browser_control/mediacapture_injection.h",
      "browser_control/popupblocker_injection.cc",
      "browser_control/popupblocker_injection.h",
      "browser_control/sitefilter_injection.cc",
      "browser_control/sitefilter_injection.h",
      "browser_control/userpermission_injection.cc",
      "browser_control/userpermission_injection.h",
    ]
  }

  if (enable_sample_webapi) {
    sources += [
      "sample/sample_injection.cc",
      "sample/sample_injection.h",
      "sample/sample_js.inc",
    ]
  }

  if (enable_webos_system_webapi) {
    sources += [
      "webossystem/cursor_injection.cc",
      "webossystem/cursor_injection.h",
      "webossystem/webossystem_datamanager.cc",
      "webossystem/webossystem_datamanager.h",
      "webossystem/webossystem_injection.cc",
      "webossystem/webossystem_injection.h",
      "webossystem/window_injection.cc",
      "webossystem/window_injection.h",
    ]
  }

  if (enable_webos_service_bridge_webapi) {
    sources += [
      "webosservicebridge/webosservicebridge_injection.cc",
      "webosservicebridge/webosservicebridge_injection.h",
      "webosservicebridge/webosservicebridge_properties.cc",
      "webosservicebridge/webosservicebridge_properties.h",
    ]
  }

  if (enable_memorymanager_webapi) {
    sources += [
      "memorymanager/memorymanager_injection.cc",
      "memorymanager/memorymanager_injection.h",
    ]
  }

  if (enable_browser_shell) {
    sources += [
      "browser_shell/browser_shell_dialog_controller.cc",
      "browser_shell/browser_shell_dialog_controller.h",
      "browser_shell/browser_shell_injection.cc",
      "browser_shell/browser_shell_injection.h",
      "browser_shell/browser_shell_ipc_injection.cc",
      "browser_shell/browser_shell_ipc_injection.h",
      "browser_shell/browser_shell_login.cc",
      "browser_shell/browser_shell_login.h",
      "browser_shell/browser_shell_page_contents.cc",
      "browser_shell/browser_shell_page_contents.h",
      "browser_shell/browser_shell_page_view.cc",
      "browser_shell/browser_shell_page_view.h",
      "browser_shell/browser_shell_permission_request.cc",
      "browser_shell/browser_shell_permission_request.h",
      "browser_shell/browser_shell_session.cc",
      "browser_shell/browser_shell_session.h",
      "browser_shell/browser_shell_webrequest.cc",
      "browser_shell/browser_shell_webrequest.h",
      "browser_shell/browser_shell_window.cc",
      "browser_shell/browser_shell_window.h",
    ]

    deps += [ "//ui/gfx/geometry" ]
  }

  if (use_gav) {
    sources += [
      "webosgavplugin/videowindow_impl.cc",
      "webosgavplugin/videowindow_impl.h",
      "webosgavplugin/webosgavplugin_datamanager.cc",
      "webosgavplugin/webosgavplugin_datamanager.h",
      "webosgavplugin/webosgavplugin_injection.cc",
      "webosgavplugin/webosgavplugin_injection.h",
    ]

    deps += [ "//content/public/common:frame_video_window_factory_mojom" ]
  }

  if (use_lttng) {
    deps += [
      "//base/trace_event/neva/lttng:webossystem_lttng_provider",
    ]
  }

  if (use_neva_chrome_extensions) {
    sources += [
      "chrome_extensions/chrome_extensions_injection.cc",
      "chrome_extensions/chrome_extensions_injection.h",
    ]
  }

  # FIXME(neva): Temporary workaround to fix build issues on Chromium v.84
  # with Neva Media enabled.
  # Need to revise dependency on //content from //neva/injection.
  if (use_neva_media) {
    deps += [ "//ui/platform_window:mojom" ]
  }
}

grit("injection_resources_grit") {
  visibility = [ ":*" ]
  source = "//neva/injection/renderer/resources/injection_resources.grd"
  outputs = [
    "grit/injection_resources.h",
    "injection_resources.pak",
  ]
}

copy("copy_injection_resources") {
  sources = [
    "$target_gen_dir/injection_resources.pak",
    "$target_gen_dir/injection_resources.pak.info",
  ]
  outputs = [
    "$root_gen_dir/injection/renderer/resources/{{source_file_part}}",
  ]

  public_deps = [
    ":injection_resources_grit",
  ]
}

group("resources") {
  public_deps = [
    ":copy_injection_resources",
  ]
}
